def gitCommit() {
    sh "git rev-parse HEAD > GIT_COMMIT-${env.BUILD_NUMBER}"
    def gitCommit = readFile("GIT_COMMIT-${env.BUILD_NUMBER}").trim()
    sh "rm -f GIT_COMMIT-${env.BUILD_NUMBER}"
    return gitCommit
}

def gitEmail() {
    sh "git --no-pager show -s --format='%ae' ${gitCommit()} > GIT_EMAIL-${env.BUILD_NUMBER}"
    def gitEmail = readFile("GIT_EMAIL-${env.BUILD_NUMBER}").trim()
    sh "rm -f GIT_EMAIL-${env.BUILD_NUMBER}"
    return gitEmail
}

def ipAddress() {
    sh "docker inspect test-container | jq -r '.[0].NetworkSettings.IPAddress' > IP_ADDRESS"
    def ipAddress = readFile("IP_ADDRESS").trim()
    sh "rm -f IP_ADDRESS"
    return ipAddress
}


node {
    // Checkout source code from Git
    stage 'Checkout'
    checkout scm


    // Build Docker image
    stage 'Build'
    sh "docker build -t mesosphere/cd-demo-app:${gitCommit()} ."


    // Test Docker image
    stage 'Test'
    sh "docker run -d --name=test-container mesosphere/cd-demo-app:${gitCommit()} jekyll serve"
    sh "docker run mesosphere/linkchecker linkchecker --no-warnings http://${ipAddress()}:4000/"


    // Log in and push image to Docker Hub
    stage 'Publish'
    withCredentials(
        [[
            $class: 'UsernamePasswordMultiBinding',
            credentialsId: 'docker-hub-credentials',
            passwordVariable: 'DOCKER_HUB_PASSWORD',
            usernameVariable: 'DOCKER_HUB_USERNAME'
        ]]
    ) {
        sh "docker login -u '${env.DOCKER_HUB_USERNAME}' -p '${env.DOCKER_HUB_PASSWORD}' -e demo@mesosphere.com"
        sh "docker push mesosphere/cd-demo-app:${gitCommit()}"
    }


    // Deploy
    stage 'Deploy'

    marathon(
        url: 'http://marathon.mesos:8080',
        forceUpdate: false,
        credentialsId: 'dcos-token',
        filename: 'marathon.json',
        id: 'jenkins-deployed-app',
        docker: "mesosphere/cd-demo-app:${gitCommit()}".toString(),
        labels: [[name: 'lastChangedBy', value: "${gitEmail()}".toString()]]
    )


    // Clean up
    stage 'Clean'
    sh "docker kill \$(docker ps -q)"
    sh "docker rm \$(docker ps -a -q)"
    sh "docker rmi \$(docker images -q)"
}
